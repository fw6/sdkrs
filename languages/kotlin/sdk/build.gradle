plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'com.sdkrs.sdk'
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.sdkrs'
            artifactId = 'sdk-android'

            // Determine the version from the git history.
            //
            // PRs: use the branch name.
            // Main: Grab it from `crates/sdkrs/Cargo.toml`

            def branchName = "git branch --show-current".execute().text.trim()

            if (branchName == "main") {
                def content = ['grep', '-o', '^version = ".*"', '../../Cargo.toml'].execute().text.trim()
                def match = ~/version = "(.*)"/
                def matcher = match.matcher(content)
                matcher.find()

                version = "${matcher.group(1)}-SNAPSHOT"
            } else {
                // branchName-SNAPSHOT
                version = "${branchName.replaceAll('/', '-')}-SNAPSHOT"
            }

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            name = "mavenRepository"
            url = uri("https://maven.pkg.github.com/fw6/sdkrs")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.14.0@aar'

    implementation 'androidx.core:core-ktx:1.13.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
